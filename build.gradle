apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'

sourceCompatibility = 1.8
version = '0.1alpha'

mainClassName = "drumseq.DrumSeq"
sourceSets {
    main {
        java {
            srcDir 'src/drumseq'
        }
        resources {
            srcDir 'src/data'
        }
    }
}

repositories {
    mavenCentral()
    maven { url "https://clojars.org/repo" }
    ivy {
        url "http://beadsproject.net/downloads/"
	layout 'pattern', {
	    artifact "[module].[ext]"
	}
    }
    flatDir {
        dirs "$rootProject.projectDir/libs"
    }
}
configurations {
    beads
}

dependencies {
    beads group: 'net.beadsproject', name: 'Beads_Processing', ext: 'zip'
    compile 'org.processing:core:2.2.1'
    //will not run with processing see: https://github.com/quil/quil/issues/95
    //compile 'org.jogamp.jogl:jogl-all-main:2.0.2'
    //compile 'org.jogamp.gluegen:gluegen-rt-main:2.0.2'
    //compile 'org.processing:processing-complete:3.0a10'
    //NOTE: core declares processing-complete as its parent pom, so it will still try resolve complete and end up downloading from clojars 
    //compile 'org.processing:core:3.0a10'
    compile('controlp5:controlp5:2.2.4-SNAPSHOT') {
        exclude group: 'quil', module: 'processing-core'
        exclude group: 'quil', module: 'processing-pdf'
        exclude group: 'quil', module: 'processing-dxf'
        exclude group: 'quil', module: 'processing-js'
        exclude group: 'org.clojure'
        exclude group: 'com.lowagie'
        //transitive = false
    }
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

//TODO should be changed to use gradle convention, using ant's resolve requires an internet connection
task extractBeads(type: Copy) {
    String s = configurations.beads.resolve()
    s = s.replaceAll(~/[\[\]]/, "")
     
    ant.unzip(src: s, dest:'libs', overwrite:"true") {
        patternset() {
	    include( name: 'beads_processing/beads/library/*.jar' )
	}
	mapper(type:"flatten")
    }
}

task wrapper(type: Wrapper) {
     scriptFile = 'DrumSeq'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}
